/*------------------------------------------------------------------------------
PROJECT: Guerrillas & Development
TOPIC: Local continuity testing/placebo
DATE: 
AUTHORS: 
RA: JMJR

NOTES: 
------------------------------------------------------------------------------*/

clear all 


*-------------------------------------------------------------------------------
* Preparing data
*-------------------------------------------------------------------------------
import delimited "${data}\gis\maps_interim\segm_info_dists.csv", clear
ren seg_id segm_id

keep segm_id dist_school dist_cities dist_road dist_comms

destring dist_school, replace force 

foreach var in dist_school dist_cities dist_road dist_comms{
	replace `var'=`var'/1000
}

ren (dist_school dist_cities dist_road dist_comms) (dist_school_cond dist_cities_cond dist_road_cond dist_comms_cond)

tostring segm_id, replace
replace segm_id="0"+segm_id if length(segm_id)==7

egen dist_any_inst_cond=rowmin(dist_school_cond dist_cities_cond dist_road_cond dist_comms_cond)

tempfile DISTS
save `DISTS', replace 

use "$data/night_light_13_segm_lvl_onu_91_nowater" , clear 

merge 1:1 segm_id using `DISTS', nogen 

la var dist_school_cond "Distance to School (1980)"
la var dist_cities_cond "Distance to City or Village (1945)"
la var dist_road_cond "Distance to Roads and Railway (1980)"
la var dist_comms_cond "Distance to Telecommunications (1945)"
la var dist_comms_cond "Distance to Any Institution"

*-------------------------------------------------------------------------------
* Seeting BW and weights
*-------------------------------------------------------------------------------
*Global of border FE for all estimates
gl breakfe="control_break_fe_400"
gl controls "within_control i.within_control#c.z_run_cntrl z_run_cntrl"
gl controls_resid "i.within_control#c.z_run_cntrl z_run_cntrl"

*RDD with break fe and triangular weights 
rdrobust arcsine_nl13 z_run_cntrl, all kernel(triangular)
gl h=2.266
gl ht= round(${h}, .001)
gl b=e(b_l)

*Conditional for all specifications
gl if "if abs(z_run_cntrl)<=${h}"

*Replicating triangular weights
cap drop tweights
gen tweights=(1-abs(z_run_cntrl/${h})) ${if}

*-------------------------------------------------------------------------------
* Results
*-------------------------------------------------------------------------------
gl lc1 "dist_cities_cond dist_road_cond dist_comms_cond"

mat drop _all

mat HA=J(1,4,.)
mat rown HA="Panel A"
local num1 : list sizeof global(lc1)
mat A1=J(`num1',4,1)

local r=1
mat rown A`r' = ${lc`r'}

local i=1
foreach var of global lc`r'{
	
	*Table 
	reghdfe `var' ${controls} [aw=tweights] ${if}, vce(r) a(i.${breakfe}) 

	mat R=r(table)

	mat A`r'[`i',1]=round(R[1,1], .001)
	mat A`r'[`i',2]=round(R[2,1], .001) 
	mat A`r'[`i',4]=e(N)
	summ `var' if e(sample)==1 & within_control==0, d
	mat A`r'[`i',3]=round(r(mean), .001)

	local++i
}

mat S=HA\A1
mat list S

local bc = rowsof(S)
matrix stars = J(`bc',2,0)

forvalues k = 1/`bc' {
	if S[`k',1]!=. {
		matrix stars[`k',1] = (abs(S[`k',1]/S[`k',2]) > invttail(`e(df_r)',0.1/2)) + (abs(S[`k',1]/S[`k',2]) > invttail(`e(df_r)',0.05/2)) + (abs(S[`k',1]/S[`k',2]) > invttail(`e(df_r)',0.01/2))
	}
}

matrix list stars

tempfile X X1 X2
frmttable using `X', statmat(S) ctitle("Variable (Year)", "Coefficient","SE", "Dependent Mean", "Obs") sdec(3,3,3,0) varlabels fragment tex nocenter annotate(stars) asymbol(*,**,***) hlines(1 1 1 0 0 1 1) replace  	
filefilter `X' `X1', from("{tabular}\BS\BS") to("{tabular}") replace
filefilter `X1' `X2', from("multicolumn{3}{c}") to("multicolumn{3}{l}") replace
filefilter `X2' "${tables}/rdd_lc_all_r3comment.tex", from("Panel A") to("\BStextit{Panel A:Baseline State Capacity (Before 1980)}") replace

*-------------------------------------------------------------------------------
* Results Interacting 
*-------------------------------------------------------------------------------
gen wc_cities=within_control*dist_cities_cond 
gen wc_road=within_control*dist_road_cond
gen wc_comms=within_control*dist_comms_cond
gen wc_any=within_control*dist_any_inst_cond

la var wc_cities "Control $\times$ Distance to City"
la var wc_road "Control $\times$ Distance to Roads"
la var wc_comms "Control $\times$ Distance to Comms."
la var wc_any "Control $\times$ Distance to Any"

gl mainoutcomes "arcsine_nl13 z_wi mean_educ_years"

eststo clear

local i=1
foreach yvar of global mainoutcomes {
	
	*Base Estimation
	eststo c`i': reghdfe `yvar' ${controls} wc_cities dist_cities_cond [aw=tweights] ${if}, vce(r) a(i.${breakfe}) 
	eststo r`i': reghdfe `yvar' ${controls} wc_road dist_road_cond [aw=tweights] ${if}, vce(r) a(i.${breakfe}) 
	eststo t`i': reghdfe `yvar' ${controls} wc_comms dist_comms_cond [aw=tweights] ${if}, vce(r) a(i.${breakfe}) 
	eststo a`i': reghdfe `yvar' ${controls} wc_any dist_any_inst_cond [aw=tweights] ${if}, vce(r) a(i.${breakfe}) 

	local ++i
}

*Exporting results 
esttab c1 c2 c3 using "${tables}/rdd_main_all_distcities_cond.tex", keep(within_control wc_cities) ///
se nocons star(* 0.10 ** 0.05 *** 0.01) ///
label nolines fragment nomtitle nonumbers obs nodep collabels(none) booktabs b(3) replace ///
prehead(`"\begin{tabular}{@{}l*{3}{c}}"' ///
            `"\hline \hline \toprule"'                     ///
            `" & Night Light & Wealth Index & Years of Education \\"' ///
            `" & (1) & (2) & (3)  \\"'                       ///
            `" \toprule"')  ///
    postfoot(`" \toprule"' ///
		`" Bandwidth (Km) & ${ht} & ${ht} & ${ht} \\"' ///
		`"\bottomrule \end{tabular}"') 

*Exporting results 
esttab r1 r2 r3 using "${tables}/rdd_main_all_distroad_cond.tex", keep(within_control wc_road) ///
se nocons star(* 0.10 ** 0.05 *** 0.01) ///
label nolines fragment nomtitle nonumbers obs nodep collabels(none) booktabs b(3) replace ///
prehead(`"\begin{tabular}{@{}l*{3}{c}}"' ///
            `"\hline \hline \toprule"'                     ///
            `" & Night Light & Wealth Index & Years of Education \\"' ///
            `" & (1) & (2) & (3)  \\"'                       ///
            `" \toprule"')  ///
    postfoot(`" \toprule"' ///
		`" Bandwidth (Km) & ${ht} & ${ht} & ${ht} \\"' ///
		`"\bottomrule \end{tabular}"') 
		
*Exporting results 
esttab t1 t2 t3 using "${tables}/rdd_main_all_distcomms_cond.tex", keep(within_control wc_comms) ///
se nocons star(* 0.10 ** 0.05 *** 0.01) ///
label nolines fragment nomtitle nonumbers obs nodep collabels(none) booktabs b(3) replace ///
prehead(`"\begin{tabular}{@{}l*{3}{c}}"' ///
            `"\hline \hline \toprule"'                     ///
            `" & Night Light & Wealth Index & Years of Education \\"' ///
            `" & (1) & (2) & (3)  \\"'                       ///
            `" \toprule"')  ///
    postfoot(`" \toprule"' ///
		`" Bandwidth (Km) & ${ht} & ${ht} & ${ht} \\"' ///
		`"\bottomrule \end{tabular}"') 
		
*Exporting results 
esttab a1 a2 a3 using "${tables}/rdd_main_all_distany_cond.tex", keep(within_control wc_any) ///
se nocons star(* 0.10 ** 0.05 *** 0.01) ///
label nolines fragment nomtitle nonumbers obs nodep collabels(none) booktabs b(3) replace ///
prehead(`"\begin{tabular}{@{}l*{3}{c}}"' ///
            `"\hline \hline \toprule"'                     ///
            `" & Night Light & Wealth Index & Years of Education \\"' ///
            `" & (1) & (2) & (3)  \\"'                       ///
            `" \toprule"')  ///
    postfoot(`" \toprule"' ///
		`" Bandwidth (Km) & ${ht} & ${ht} & ${ht} \\"' ///
		`"\bottomrule \end{tabular}"') 




*END


